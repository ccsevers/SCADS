ENTITY user
{
	string name,
	string password,
	string email,
	string profileData
	PRIMARY(name)
}

ENTITY thought
{
	int timestamp,
	string thought
	PRIMARY(timestamp)
}

ENTITY thoughtref
{
    int id
    PRIMARY(id)
}

ENTITY subscription
{
    int id,
	bool approved
	PRIMARY(id)
}

ENTITY topic
{
    int    id,
	string name
	PRIMARY(id)
}

RELATIONSHIP owner FROM user TO MANY thought
RELATIONSHIP following FROM user TO 5000 subscription
RELATIONSHIP target FROM user TO 1 subscription
RELATIONSHIP hashtag FROM thought TO 10 topic
RELATIONSHIP thoughtToRef FROM thought TO 10 thoughtref
RELATIONSHIP refToUser FROM user TO MANY thoughtref

QUERY myThoughts
FETCH thought
	OF user BY owner
WHERE user=[this]
LIMIT [1: numperpage] MAX 10

QUERY thoughtsByHashTag
FETCH thought OF topic BY hashtag
WHERE name=[1:name]
ORDER BY timestamp LIMIT [2:count] MAX 100

// These are people that I am following
// NOT people that are following me
QUERY myFollowing
FETCH user
	OF subscription BY target
	OF user me BY following
WHERE me=[this]
LIMIT [1:numperpage] MAX 30

QUERY thoughtstream
FETCH thought
	OF user friend BY owner
	OF subscription BY target
	OF user me BY following
WHERE me=[this] AND subscription.approved = true
ORDER BY timestamp
LIMIT [1:count] MAX 100

/*
QUERY references
FETCH thought
	OF user BY references
WHERE user=[this]
*/
// these are people that reference me in their thoughts
QUERY myReferences
FETCH thought
    OF thoughtref BY thoughtToRef
    OF user BY refToUser
WHERE user=[this]
LIMIT 10 MAX 10

QUERY needsApproval
FETCH subscription
	OF user BY target
WHERE user = [this] AND subscription.approved = false

QUERY userByName
FETCH user
WHERE user.name = [1:name]

QUERY userByEmail
FETCH user
WHERE email = [1:email]

QUERY finduserByPK
FETCH user
WHERE name = [1:name]

/* Missing Queries */
//Who's following me?
//How many people are following me?
//How many people am I following?
//How many tweets do I have?
